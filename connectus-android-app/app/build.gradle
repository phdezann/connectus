buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'me.tatarka:gradle-retrolambda:3.2.5'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.11.3'
        classpath 'com.google.gms:google-services:1.3.0-beta1'
        classpath 'com.github.triplet.gradle:play-publisher:1.1.4'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.github.triplet.play'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        abortOnError false
    }
    defaultConfig {
        applicationId 'org.connectus'
        minSdkVersion 19
        targetSdkVersion 23
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
        buildConfigField 'String', 'FIREBASE_ROOT_URL', "\"$System.env.APPLICATION_FIREBASE_URL\""
        versionName VersioningUtils.versionName()
        versionCode VersioningUtils.versionCode()
    }
    sourceSets {
        test {
            java.srcDirs += 'src/sharedTest/java'
        }
        androidTest {
            java.srcDirs += 'src/sharedTest/java'
        }
    }
    signingConfigs {
        debug {
            storeFile file("debug.keystore")
            storePassword "secret"
            keyAlias "debug"
            keyPassword "secret"
        }
        release {
            storeFile file("$System.env.ANDROID_RELEASE_KEYSTORE_PATH")
            storePassword "$System.env.ANDROID_RELEASE_KEYSTORE_PASSWORD"
            keyAlias "$System.env.ANDROID_RELEASE_KEY_ALIAS"
            keyPassword "$System.env.ANDROID_RELEASE_KEY_PASSWORD"
        }
    }
    buildTypes {
        debug {
            minifyEnabled false
            multiDexEnabled true
        }
        jrebel {
            debuggable true
            signingConfig signingConfigs.debug
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'NOTICE'
        exclude 'asm-license.txt'
    }
    dexOptions {
        javaMaxHeapSize "2g"
    }
}

play {
    jsonFile = file("$System.env.ANDROID_PLAY_PUBLISHER_SERVICE_ACCOUNT_JSON_PATH")
    track = 'alpha'
}

task('writeReleaseDescription') << {
    File configFile = file('release-description.txt')
    configFile.withWriter { out ->
        out.println """{
            "gitSha": "${VersioningUtils.gitSha()}",
            "versionName": "${VersioningUtils.versionName()}",
            "versionCode": "${VersioningUtils.versionCode()}",
            "timestamp": {".sv": "timestamp"}
            }
        """
    }
}

project.afterEvaluate {
    def publishApkRelease = project.tasks.getByName("publishApkRelease")
    publishApkRelease.dependsOn writeReleaseDescription
}

final DAGGER_VERSION = '2.0.2'

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar']),
            'com.android.support:appcompat-v7:23.1.1',
            'com.android.support:recyclerview-v7:23.1.1',
            'com.android.support:support-v13:23.1.1',
            'com.google.android.gms:play-services-auth:8.3.0',
            'joda-time:joda-time:2.8.1',
            'com.google.guava:guava:18.0',
            'commons-io:commons-io:2.4',
            'commons-codec:commons-codec:1.3',
            'org.apache.commons:commons-lang3:3.4',
            'io.reactivex:rxjava:1.0.15',
            'io.reactivex:rxandroid:1.0.1',
            "com.google.dagger:dagger:${DAGGER_VERSION}",
            'javax.annotation:javax.annotation-api:1.2',
            'org.slf4j:slf4j-jdk14:1.7.12',
            'org.slf4j:slf4j-api:1.7.12',
            'com.firebase:firebase-client-android:2.5.0',
            'com.firebaseui:firebase-ui:0.2.2'

    testCompile 'junit:junit:4.12',
            "com.google.dagger:dagger-compiler:${DAGGER_VERSION}",
            'org.robolectric:robolectric:3.0',
            'com.squareup.assertj:assertj-android:1.1.1',
            'org.mockito:mockito-all:1.10.19'

    provided 'org.projectlombok:lombok:1.16.4'

    compile 'com.android.support:support-annotations:23.1.1'
    testCompile 'com.android.support:support-annotations:23.1.1'

    androidTestCompile 'com.android.support:support-annotations:23.1.1',
            'com.android.support.test:runner:0.4.1',
            'com.android.support.test:rules:0.4.1',
            'com.android.support.test.espresso:espresso-core:2.2.1',
            'com.android.support.test.espresso:espresso-idling-resource:2.2.1',
            'com.google.dexmaker:dexmaker-mockito:1.2'
    androidTestCompile('com.android.support.test.espresso:espresso-contrib:2.2.1') {
        exclude module: 'support-v4'
    }

    androidTestApt "com.google.dagger:dagger-compiler:${DAGGER_VERSION}"
    apt "com.google.dagger:dagger-compiler:${DAGGER_VERSION}"
}
