buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'me.tatarka:gradle-retrolambda:3.2.0'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.11.3'
        classpath 'com.google.gms:google-services:1.3.0-beta1'
        classpath 'com.github.triplet.gradle:play-publisher:1.1.4'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.github.triplet.play'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        abortOnError false
    }
    defaultConfig {
        applicationId 'org.connectus'
        minSdkVersion 19
        targetSdkVersion 23
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'

        def longVersionName = "git -C ${rootDir} describe --tags --long".execute().text.trim()
        def (fullVersionTag, versionBuild, gitSha) = longVersionName.tokenize('-')
        def(versionMajor, versionMinor, versionPatch) = fullVersionTag.tokenize('.')

        versionName "$versionMajor.$versionMinor.$versionPatch($versionBuild)"
        versionCode versionMajor.toInteger() * 100000 +
                versionMinor.toInteger() * 10000 +
                versionPatch.toInteger() * 1000 +
                versionBuild.toInteger()
    }
    signingConfigs {
        release {
            if (System.getenv()["CI"]) {
                storeFile file(System.getenv()["KEYSTORE_PATH"])
                storePassword System.getenv()["KEYSTORE_PASSWORD"]
                keyAlias System.getenv()["KEY_ALIAS"]
                keyPassword System.getenv()["KEY_PASSWORD"]
            } else {
                storeFile file(System.properties['user.home'] + "/.android-private/connectus-release.keystore")
                storePassword "secret"
                keyAlias "release"
                keyPassword "secret"
            }
        }
    }
    buildTypes {
        debug {
            minifyEnabled false
            multiDexEnabled true
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'NOTICE'
        exclude 'asm-license.txt'
    }
    dexOptions {
        javaMaxHeapSize "2g"
    }
}

play {
    if (System.getenv()["CI"]) {
        jsonFile = file(System.getenv()["PLAY_PUBLISHER_ACCOUNT_PATH"])
    } else {
        jsonFile = file(System.properties['user.home'] + "/.android-private/connectus-play-publisher-account.json")
    }
    track = 'alpha'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.1.1'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar']),
            'com.android.support:appcompat-v7:23.1.1',
            'com.android.support:recyclerview-v7:23.1.1',
            'com.android.support:support-v13:23.1.1',
            'com.google.android.gms:play-services:7.5.0',
            'com.google.android.gms:play-services-analytics:7.3.0',
            'joda-time:joda-time:2.8.1',
            'com.google.guava:guava:18.0',
            'commons-io:commons-io:2.4',
            'commons-codec:commons-codec:1.3',
            'org.apache.commons:commons-lang3:3.4',
            'io.reactivex:rxjava:1.0.15',
            'io.reactivex:rxandroid:1.0.1',
            'org.slf4j:slf4j-jdk14:1.7.12',
            'org.slf4j:slf4j-api:1.7.12'

    testCompile 'junit:junit:4.12',
            'org.robolectric:robolectric:3.0',
            'org.mockito:mockito-all:1.10.19'

    provided 'org.projectlombok:lombok:1.16.4'

    compile 'com.android.support:support-annotations:23.1.1'
    testCompile 'com.android.support:support-annotations:23.1.1'
    androidTestCompile 'com.android.support:support-annotations:23.1.1'

    androidTestCompile 'com.android.support.test:runner:0.4.1',
            'com.android.support.test:rules:0.4.1',
            'com.android.support.test.espresso:espresso-core:2.2.1',
            'com.google.dexmaker:dexmaker-mockito:1.2'
    androidTestCompile('com.android.support.test.espresso:espresso-contrib:2.2.1') {
        exclude module: 'support-v4'
    }
}
